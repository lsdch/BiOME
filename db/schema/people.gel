# Module people holds type definitions related to users, persons and organisations.
#
module people {

  function personByAlias(alias: str) -> Person {
    using (
      select assert_exists(
        Person filter .alias = alias,
        message := "Failed to find person with alias: " ++ alias
      )
    );
  };

  scalar type OrgKind extending enum<Lab, FundingAgency, SequencingPlatform, Other>;

  type Organisation extending default::Auditable {
    required name: str {
      constraint exclusive;
      constraint min_len_value(3);
      constraint max_len_value(128);
    };
    required code: str {
      constraint exclusive;
      constraint min_len_value(2);
      constraint max_len_value(32);
    };
    description: str {
      rewrite insert, update using (default::null_if_empty(.description));
    };

    required kind: OrgKind;

    multi link people := .<organisations[is Person];
  }

  function insert_organisation(data: json) -> Organisation {
    using (
      insert Organisation {
        name := <str>data['name'],
        code := <str>data['code'],
        kind := <OrgKind>data['kind'],
        description := <str>json_get(data, 'description'),
      }
    );
  };

  function insert_or_find_organisation(data: json) -> Organisation {
    using (select(
      if (json_typeof(data) = 'object')
      then (select (insert_organisation(data)))
      else if (json_typeof(data) = 'string') then (
        select (
          assert_exists(
            Organisation filter .code = <str>data,
            message := "Failed to find organisation with code: " ++ <str>data
          )
        )
      )
      else (
        assert_exists(<Organisation>{},
        message := "Invalid organisation JSON type: " ++ json_typeof(data))
      )
    ));
  };

  type Person extending default::Auditable {
    required first_name: str {
      # constraint min_len_value(1);
      constraint max_len_value(30);
    };
    required last_name: str {
      constraint min_len_value(2);
      constraint max_len_value(30);
    };
    required property full_name := (select(assert_exists(
      if len(.first_name) > 0
      then array_join([.first_name, .last_name], ' ')
      else .last_name
    )));
    required alias: str {
      constraint exclusive;
      constraint min_len_value(2);
      constraint max_len_value(32);
      default := <str>{};
      rewrite insert, update using (
        default::null_if_empty(.alias) ?? (
        with
          default_alias := (
            if len(.first_name) >0
            then str_lower(.first_name[0] ++ .last_name)
            else str_lower(.last_name)
          ),
          conflicts := (select count (
            detached Person filter (
              str_trim(.alias, "0123456789") = default_alias
            )
          )),
          suffix := if conflicts > 0 then <str>(conflicts) else "",
        select (default_alias ++ suffix)
        )
      );
    };

    index on ((.alias, .last_name));

    contact: str {
      rewrite insert, update using (default::null_if_empty(.contact));
    };
    multi organisations: Organisation;

    comment : str;

    link user := .<identity[is User];

    optional property role := .user.role;
  }

  function insert_person(data: json) -> Person {
    using (
      insert Person {
        first_name := <str>data['first_name'],
        last_name := <str>data['last_name'],
        alias := <str>json_get(data, 'alias'),
        contact := <str>json_get(data, 'contact'),
        comment := <str>json_get(data, 'comment'),
        organisations := (
          distinct (for org in json_array_unpack(json_get(data, 'organisations')) union (
            assert_exists(
              Organisation filter .code = <str>org,
              message := "Failed to find organisation with code: " ++ <str>org
            )
          ))
        )
      }
    );
  };


  scalar type UserRole extending enum<Visitor, Contributor, Maintainer, Admin>;


  type User {
    required login: str {
      constraint exclusive;
      constraint min_len_value(5);
      constraint max_len_value(16);
    };
    required email: str {
      constraint exclusive;
    };

    index on ((.email, .login));

    required password: str {
      annotation description := "Password hashing is done within the database, raw password must be used when creating/updating.";
      rewrite insert, update using (
        if __specified__.password
        then ext::pgcrypto::crypt(.password, ext::pgcrypto::gen_salt('bf', 10))
        else .password
      );
    };

    required role: UserRole {
      default := UserRole.Visitor;
    };

    required identity: Person {
      constraint exclusive;
      on target delete restrict;
      on source delete allow;
    };
  }

  type PendingUserRequest {
    required email: str {
      constraint exclusive;
    };
    index on (.email);
    required first_name: str;
    required last_name: str;
    required full_name := .first_name ++ " " ++ .last_name;
    organisation: str;
    motive: str;
    required created_on: datetime {
      rewrite insert using (datetime_of_statement());
    };
    required email_verified: bool {
      default := false;
    };
  }
}