module datasets {

  type ResearchProgram extending default::Auditable {
    required label: str { constraint exclusive };
    required code: str { constraint exclusive };

    required multi managers: people::Person;
    multi funding_agencies: people::Organisation;

    start_year: int32 {
      constraint min_value(1900);
    };
    end_year: int32;
    constraint expression on (.start_year <= .end_year);

    description: str;

    multi datasets: Dataset {
      on target delete allow;
      on source delete allow;
    };
  };

  scalar type DatasetCategory extending enum<Site, Occurrence, Seq>;

  abstract type Dataset extending default::Auditable {
    required label: str {
      constraint min_len_value(4);
      constraint max_len_value(40);
    };
    required slug: str {
      constraint exclusive;
    };
    description: str;

    required pinned: bool {
      default := false;
    };

    required multi maintainers: people::Person {
      # gel error: SchemaDefinitionError:
      # cannot specify a rewrite for link 'maintainers' of object type 'location::SiteDataset' because it is multi
      # Hint: this is a temporary implementation restriction
      # Currently handling this in the application layer
      # rewrite insert using (global default::current_user.identity union .maintainers);
      # rewrite update using (.maintainers union .meta.created_by_user.identity);
    };
    required category := <DatasetCategory>str_replace(
      str_split(.__type__.name, "::")[1],
      "Dataset", ""
    );
  }

  type SiteDataset extending Dataset {
    multi sites: location::Site {
      on target delete allow;
      on source delete allow;
    };
  };

  type OccurrenceDataset extending Dataset {
    multi occurrences: occurrence::Occurrence {
      on target delete allow;
      on source delete allow;
    };
    multi sites := (.occurrences.sampling.event.site);

    # Dataset is congruent if all occurrences are congruent:
    # Morphological identification matches molecular identification
    required is_congruent := (all(
      (select .occurrences {
        is_congruent := (
          [is occurrence::ExternalBioMat].is_congruent ??
          [is occurrence::InternalBioMat].is_congruent ??
          true
        )
      }).is_congruent
    ));
  }

  type SeqDataset extending Dataset {
    multi sequences: seq::Sequence {
      on target delete allow;
      on source delete allow;
    };
    multi sites := (.sequences.sampling.event.site)
  }


  type MOTUDataset extending default::Auditable {
    required label: str;
    multi link MOTUs := .<dataset[is MOTU];
    published_in: references::Article;
    required multi generated_by: people::Person;
    comments: str;
  }

  type DelimitationMethod extending default::Vocabulary, default::Auditable;

  type MOTU extending default::Auditable {
    required dataset: MOTUDataset;
    required number: int32;
    required method: DelimitationMethod;
    required multi sequences: seq::Sequence;
  }
}